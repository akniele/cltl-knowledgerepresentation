PREFIX n2mu: <http://cltl.nl/leolani/n2mu/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gaf: <http://groundedannotationframework.org/gaf#>
PREFIX grasp: <http://groundedannotationframework.org/grasp#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX graspf: <http://groundedannotationframework.org/grasp/factuality#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/>

select ?val ?authorlabel ?date
where {
    BIND(<%s> AS ?p) .
    BIND(<%s> AS ?s) .
    BIND(<%s> AS ?o) .

    GRAPH ?g {
        ?s ?p ?o .
    } .

    ?g gaf:denotedBy ?m .
    ?m grasp:hasAttribution ?att .
    ?att rdf:value ?val .
    ?val rdf:type graspf:PolarityValue .

    ?m prov:wasDerivedFrom ?utt .
    ?utt rdf:type grasp:Utterance .
    ?chat sem:hasSubEvent ?utt .
    ?cont sem:hasEvent ?chat .
    ?cont ?time_pred ?date .
    VALUES (?time_pred) { (sem:hasTime) (sem:hasBeginTimeStamp) } .

    ?m grasp:wasAttributedTo ?author .
    ?author rdfs:label ?authorlabel .
}
GROUP BY ?val ?authorlabel ?date